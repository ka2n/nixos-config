# bwrap wrapper for Intune packages
# Provides fake Ubuntu 24.04.3 LTS os-release/lsb-release
{ writeShellScript, bubblewrap, writeTextDir, symlinkJoin }:

let
  ubuntuRelease = symlinkJoin {
    name = "ubuntu-release-files";
    paths = [
      (writeTextDir "os-release" ''
        PRETTY_NAME="Ubuntu 24.04.3 LTS"
        NAME="Ubuntu"
        VERSION_ID="24.04"
        VERSION="24.04.3 LTS (Noble Numbat)"
        VERSION_CODENAME=noble
        ID=ubuntu
        ID_LIKE=debian
        HOME_URL="https://www.ubuntu.com/"
        SUPPORT_URL="https://help.ubuntu.com/"
        BUG_REPORT_URL="https://bugs.launchpad.net/ubuntu/"
        PRIVACY_POLICY_URL="https://www.ubuntu.com/legal/terms-and-policies/privacy-policy"
        UBUNTU_CODENAME=noble
        LOGO=ubuntu-logo
      '')
      (writeTextDir "lsb-release" ''
        DISTRIB_ID=Ubuntu
        DISTRIB_RELEASE=24.04
        DISTRIB_CODENAME=noble
        DISTRIB_DESCRIPTION="Ubuntu 24.04.3 LTS"
      '')
    ];
  };
in
{
  makeWrapper = bin: writeShellScript (baseNameOf bin) ''
    exec ${bubblewrap}/bin/bwrap \
      --bind / / \
      --dev-bind /dev /dev \
      --ro-bind ${ubuntuRelease}/lsb-release /etc/lsb-release \
      --ro-bind ${ubuntuRelease}/os-release /etc/os-release \
      -- ${bin} "$@"
  '';
}
